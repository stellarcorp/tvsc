load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_pkg//pkg:deb.bzl", "pkg_deb")
load("//platforms:platform.bzl", "arch_name")
load("//service:version.bzl", "create_service_dependency_string", "tarball_name", "version_string")

cc_binary(
    name = "tvsc_proxy",
    srcs = [
        "tvsc_proxy.cc",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//discovery:resolve",
        "//http",
        "//pubsub",
        "//pubsub:web_socket_pub_sub",
        "//service/communications/client",
        "//service/communications/client:web_socket_rpc_client",
        "//service/datetime/client",
        "//service/datetime/client:web_socket_client",
        "//service/echo/client:web_socket_rpc_client",
        "//service/hello/client:web_socket_rpc_client",
        "//third_party/gflags",
        "//third_party/glog",
        "//third_party/grpc",
        "//third_party/uwebsockets",
    ],
)

pkg_files(
    name = "binaries",
    testonly = True,
    srcs = [
        ":tvsc_proxy",
    ],
    attributes = pkg_attributes(
        mode = "0755",
    ),
    prefix = "usr/bin",
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "systemd_files",
    srcs = [
        ":etc/tvsc-proxy.service",
    ],
    attributes = pkg_attributes(
        mode = "0644",
    ),
    prefix = "lib/systemd/system",
    visibility = ["//visibility:public"],
)

PACKAGE_NAME = "tvsc-proxy"

pkg_tar(
    name = "tarball",
    testonly = True,
    srcs = [
        ":binaries",
        ":systemd_files",
    ],
    package_file_name = tarball_name(PACKAGE_NAME),
)

pkg_deb(
    name = "deb",
    testonly = True,
    architecture = arch_name(),
    data = ":tarball",
    depends = [
        "libavahi-client3",
        "avahi-daemon",
        "iptables (>= 1.6)",
        "iptables-persistent (>= 1.0.11)",
        "libwiringpi2",
        create_service_dependency_string("tvsc-datetime"),
        create_service_dependency_string("tvsc-echo"),
        create_service_dependency_string("tvsc-hello"),
        create_service_dependency_string("tvsc-communications"),
    ],
    description = "Reverse network proxy package from the Virtual Tower system from TVSC",
    maintainer = "info@stellarcorp.tv",
    package = PACKAGE_NAME,
    postinst = "debian/tvsc_proxy_service.postinst",
    preinst = "debian/tvsc_proxy_service.preinst",
    prerm = "debian/tvsc_proxy_service.prerm",
    version = version_string(),
    visibility = ["//visibility:public"],
)
