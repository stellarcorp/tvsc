load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_filegroup", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_pkg//pkg:deb.bzl", "pkg_deb")
load("//platforms:platform.bzl", "arch_name")
load("//service:version.bzl", "tarball_name", "version_string")

pkg_files(
    name = "binaries",
    testonly = True,
    srcs = [
        "//service/datetime/client:datetime_client",
        "//service/datetime/server:datetime_server",
    ],
    attributes = pkg_attributes(
        mode = "0755",
    ),
    prefix = "usr/bin",
)

pkg_files(
    name = "systemd_files",
    srcs = [
        ":etc/tvsc-datetime.service",
    ],
    attributes = pkg_attributes(
        mode = "0644",
    ),
    prefix = "lib/systemd/system",
)

PACKAGE_NAME = "tvsc-datetime"

pkg_tar(
    name = "tarball",
    testonly = True,
    srcs = [
        ":binaries",
        ":systemd_files",
    ],
    package_file_name = tarball_name(PACKAGE_NAME),
)

pkg_deb(
    name = "deb",
    testonly = True,
    architecture = arch_name(),
    data = ":tarball",
    depends = [
        "libavahi-client3",
        "avahi-daemon",
    ],
    description = "Datetime package from the Virtual Tower system from TVSC",
    maintainer = "info@stellarcorp.tv",
    package = PACKAGE_NAME,
    postinst = "debian/tvsc_datetime_service.postinst",
    preinst = "debian/tvsc_datetime_service.preinst",
    prerm = "debian/tvsc_datetime_service.prerm",
    version = version_string(),
    visibility = ["//visibility:public"],
)

cc_test(
    name = "datetime_service_test",
    srcs = [
        "datetime_service_test.cc",
    ],
    deps = [
        "//service/datetime/client",
        "//service/datetime/server",
        "//service/utility",
        "//service/utility:testing",
        "//third_party/glog",
        "//third_party/gtest",
    ],
)
