load("//platforms:hex.bzl", "hex")

# GPIO targets
cc_library(
    name = "gpio",
    target_compatible_with = select({
        "//platforms:wiring_pi_gpio": [
        ],
        "//platforms:teensyduino_gpio": [
        ],
        "//platforms:no_gpio": [
            "@platforms//:incompatible",
        ],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
    visibility = ["//visibility:public"],
    deps = [":gpio_headers"] + select({
        "//platforms:teensyduino_gpio": [
            ":teensyduino_gpio",
        ],
        "//platforms:wiring_pi_gpio": [
            ":wiring_pi_gpio",
        ],
    }),
)

cc_library(
    name = "gpio_headers",
    hdrs = [
        "interrupts.h",
        "pins.h",
    ],
)

cc_library(
    name = "teensyduino_gpio",
    srcs = [
        "teensyduino_interrupts.cc",
        "teensyduino_pins.cc",
    ],
    target_compatible_with = select({
        "//platforms:teensyduino_gpio": [
        ],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
    deps = [
        ":gpio_headers",
        "//base",
        "//third_party/arduino:core",
        "//third_party/arduino:gpio",
    ],
)

cc_library(
    name = "wiring_pi_gpio",
    srcs = [
        "wiring_pi_interrupts.cc",
        "wiring_pi_pins.cc",
    ],
    target_compatible_with = select({
        "//platforms:wiring_pi_gpio": [
        ],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
    deps = [
        ":gpio_headers",
        "//base",
        "//third_party/wiring_pi",
    ],
)

# Blink example to verify GPIO.
cc_binary(
    name = "blink",
    srcs = ["blink.cc"],
    deps = [
        ":gpio",
        "//hal/output",
        "//hal/time",
    ] + select({
        "//platforms:teensy40_board": [
            ":blink_pin_mapping_teensy_default",
        ],
        "//platforms:teensy41_board": [
            ":blink_pin_mapping_teensy_default",
        ],
        "//platforms:odroid_m1_board": [
            ":blink_pin_mapping_odroid_default",
        ],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "blink_pin_mapping",
    hdrs = [
        "blink_pin_mapping.h",
    ],
)

cc_library(
    name = "blink_pin_mapping_teensy_default",
    srcs = [
        "blink_pin_mapping_teensy_default.cc",
    ],
    target_compatible_with = select({
        "//platforms:teensy40_board": [
        ],
        "//platforms:teensy41_board": [
        ],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
    deps = [
        ":blink_pin_mapping",
        "//third_party/arduino:core",
    ],
)

cc_library(
    name = "blink_pin_mapping_odroid_default",
    srcs = [
        "blink_pin_mapping_odroid_default.cc",
    ],
    target_compatible_with = select({
        "//platforms:odroid_m1_board": [
        ],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
    deps = [
        ":blink_pin_mapping",
    ],
)

hex(
    name = "blink_hex",
    src = ":blink",
    out = "blink.hex",
    target_compatible_with = select({
        "@platforms//os:none": [
        ],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
)
