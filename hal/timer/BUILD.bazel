cc_library(
    name = "timer_headers",
    hdrs = [
        "timer.h",
    ],
    deps = [
        "//hal/gpio",
    ],
)

cc_library(
    name = "timer",
    target_compatible_with = select({
        "//platforms:stm32_core": [],
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":timer_headers",
        "//hal",
    ] + select({
        "//platforms:stm32_core": [
            ":stm32l4xx_timer",
        ],
        "@platforms//os:linux": [
            ":simulation",
        ],
    }),
)

cc_library(
    name = "stm32l4xx_timer",
    srcs = [
        "stm32l4xx_timer.cc",
    ],
    hdrs = [
        "stm32l4xx_timer.h",
    ],
    target_compatible_with = [
        "//platforms:stm32_core",
    ],
    deps = [
        ":timer_headers",
        "//hal",
        "//hal/rcc",
        "//third_party/stm32",
    ],
)

cc_library(
    name = "simulation",
    srcs = [
        "timer_noop.cc",
    ],
    hdrs = [
        "timer_interceptor.h",
        "timer_noop.h",
    ],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
    deps = [
        ":timer_headers",
        "//hal",
    ],
)
