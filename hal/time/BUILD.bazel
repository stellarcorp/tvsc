cc_library(
    name = "time",
    srcs = [
        "clock.cc",
        "remote_clock.cc",
    ],
    hdrs = [
        "remote_clock.h",
    ],
    target_compatible_with = select({
        "//platforms:teensyduino_core": [],
        "//platforms:stm32_core": [],
        "//platforms:std_cpp": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":time_headers",
    ] + select({
        "//platforms:teensyduino_core": [
            ":teensyduino_time",
        ],
        "//platforms:stm32_core": [
            ":stm32_time",
        ],
        "//platforms:std_cpp": [
            ":std_cpp_time",
        ],
    }),
)

cc_library(
    name = "time_headers",
    hdrs = [
        "clock.h",
    ],
)

cc_library(
    name = "teensyduino_time",
    srcs = [
        "teensyduino_clock.cc",
    ],
    hdrs = [
        "teensyduino_clock.h",
    ],
    target_compatible_with = [
        "//platforms:teensyduino_core",
    ],
    deps = [
        ":time_headers",
        "//third_party/arduino:core",
    ],
)

cc_library(
    name = "stm32_time",
    srcs = [
        "stm_clock.cc",
    ],
    hdrs = [
        "stm_clock.h",
    ],
    deps = [
        # Note that we do not include a dependency on the STM32 HAL. Where necessary, we forward
        # declare globals, types, and functions in the code here. This allows us to test the clock
        # on x86_64, if we fake SysTick interrupts.
        # Also, we don't restrict the use of this library to just STM32 MCUs via
        # target_compatible_with(). Again, this is to allow us to test this code on the desktop.
        ":time_headers",
    ],
)

cc_library(
    name = "std_cpp_time",
    srcs = [
        "std_cpp_clock.cc",
    ],
    hdrs = [
        "std_cpp_clock.h",
    ],
    target_compatible_with = [
        "//platforms:std_cpp",
    ],
    deps = [
        ":time_headers",
    ],
)

cc_library(
    name = "mock_clock",
    testonly = True,
    srcs = [
        "mock_clock.cc",
    ],
    hdrs = [
        "mock_clock.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":time_headers",
    ],
)

cc_test(
    name = "mock_clock_test",
    srcs = [
        "mock_clock_test.cc",
    ],
    deps = [
        ":mock_clock",
        "//third_party/gtest",
    ],
)

cc_test(
    name = "remote_clock_test",
    srcs = [
        "remote_clock_test.cc",
    ],
    deps = [
        ":mock_clock",
        ":time",
        "//third_party/gtest",
    ],
)

cc_test(
    name = "stm32_clock_test",
    srcs = [
        "stm_clock_test.cc",
    ],
    deps = [
        ":stm32_time",
        "//third_party/gtest",
    ],
)
