load("@rules_cc//cc:defs.bzl", "cc_library", "cc_proto_library")
load("@rules_proto//proto:defs.bzl", "proto_library")

cc_library(
    name = "tdma",
    srcs = [
        "tdma_schedule.cc",
    ],
    hdrs = [
        "tdma_schedule.h",
        "tdma_transceiver.h",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":tdma_frame",
        ":tdma_serialization",
        "//base",
        "//comms/packet",
        "//comms/radio",
        "//comms/route",
        "//hal/output",
        "//hal/time",
    ],
)

cc_library(
    name = "tdma_serialization",
    visibility = ["//visibility:public"],
    deps = [":tdma_serialization_headers"] + select({
        "@platforms//os:none": [
            ":tdma_serialization_nanopb",
        ],
        "//conditions:default": [
            ":tdma_serialization_proto",
        ],
    }),
)

cc_library(
    name = "tdma_frame",
    srcs = [
        "tdma_frame.cc",
    ],
    hdrs = [
        "tdma_frame.h",
    ],
    deps = [
        "//base",
    ],
)

cc_library(
    name = "tdma_serialization_headers",
    hdrs = [
        "tdma_serialization.h",
    ],
    deps = [
        ":tdma_frame",
    ],
)

cc_library(
    name = "tdma_serialization_proto",
    srcs = [
        "tdma_serialization_proto.cc",
    ],
    target_compatible_with = select({
        "@platforms//os:none": [
            "@platforms//:incompatible",
        ],
        "//conditions:default": [],
    }),
    deps = [
        ":tdma_serialization_headers",
        "//comms/tdma/proto:tdma_frame_cc_proto",
    ],
)

cc_library(
    name = "tdma_serialization_nanopb",
    srcs = [
        "tdma_serialization_nanopb.cc",
    ],
    target_compatible_with = select({
        "@platforms//os:none": [],
        "//conditions:default": [
            "@platforms//:incompatible",
        ],
    }),
    deps = [
        ":tdma_serialization_headers",
        "//comms/tdma/nanopb_proto:tdma_frame_nano_cc_proto",
    ],
)

cc_test(
    name = "tdma_schedule_test",
    srcs = ["tdma_schedule_test.cc"],
    deps = [
        ":tdma",
        "//hal/time:mock_clock",
        "//third_party/gtest",
    ],
)

cc_test(
    name = "tdma_serialization_test",
    srcs = ["tdma_serialization_test.cc"],
    deps = [
        ":tdma_serialization",
        "//third_party/gtest",
    ],
)

cc_test(
    name = "tdma_transceiver_test",
    srcs = ["tdma_transceiver_test.cc"],
    deps = [
        ":tdma",
        "//comms/radio:mock_radio",
        "//hal/time:mock_clock",
        "//third_party/gtest",
    ],
)
