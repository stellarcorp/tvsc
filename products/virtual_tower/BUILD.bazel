load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_pkg//pkg:deb.bzl", "pkg_deb")
load("//platforms:platform.bzl", "arch_name")
load("//service:version.bzl", "tarball_name", "version_string")

PACKAGE_NAME = "tvsc-virtual-tower"

pkg_files(
    name = "ui_files",
    testonly = True,
    srcs = [
        "//products/virtual_tower/ui",
    ],
    attributes = pkg_attributes(
        mode = "0644",
    ),
    prefix = "usr/share/tvsc/virtual_tower/ui",
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "systemd_files",
    srcs = [
        ":etc/tvsc-virtual-tower.service",
    ],
    attributes = pkg_attributes(
        mode = "0644",
    ),
    prefix = "lib/systemd/system",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tarball",
    testonly = True,
    srcs = [
        ":systemd_files",
        ":ui_files",
        "//service/communications:binaries",
        "//service/communications:systemd_files",
        "//service/datetime:binaries",
        "//service/datetime:systemd_files",
        "//service/echo:binaries",
        "//service/echo:systemd_files",
        "//service/hello:binaries",
        "//service/hello:systemd_files",
        "//service/proxy:binaries",
    ],
    package_file_name = tarball_name(PACKAGE_NAME),
)

pkg_deb(
    name = "deb",
    testonly = True,
    architecture = arch_name(),
    data = ":tarball",
    depends = [
        "libavahi-client3",
        "avahi-daemon",
        "iptables (>= 1.6)",
        "iptables-persistent (>= 1.0.11)",
        "libwiringpi2",
    ],
    description = "Virtual Tower system from TVSC. This system provides communications " +
                  "facilities for constructing a temporary communications tower using an " +
                  "unmanned aerial vehicle such as a drone, weather balloon or similar device.",
    maintainer = "info@stellarcorp.tv",
    package = PACKAGE_NAME,
    postinst = "debian/tvsc_virtual_tower.postinst",
    preinst = "debian/tvsc_virtual_tower.preinst",
    prerm = "debian/tvsc_virtual_tower.prerm",
    version = version_string(),
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "run_virtual_tower",
    srcs = ["run_virtual_tower.sh"],
    data = [
        "//service/communications/server:tvsc-communications-service",
        "//service/datetime/server:datetime_server",
        "//service/echo/server:echo_server",
        "//service/hello/server:hello_server",
        "//service/proxy:tvsc_proxy",
    ],
)
